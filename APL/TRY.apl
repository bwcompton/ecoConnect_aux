Z⍙⍙←A⍙⍙ TRY X⍙⍙;⎕ELX
⍝Execute ⍵ and return 1 if error, 0 if successful; don't trap if ⍺=0, don't trap but do report if ⍺=¯1
⍝Error message is in ⎕DM
⍝B. Compton, 21 Apr 2011
⍝22 Jun 2011: Pass BREAKCHECK errors through
⍝25 Apr 2012: if ⍺=¯1, REPORT error
⍝27 Feb 2024: on non-development machines under APL64, always report when not trapping



 ⍎(0=⎕NC'A⍙⍙')/'A⍙⍙←1'
 

 :if A⍙⍙=0                                          ⍝If trapping is turned off
 :andif APL64                                       ⍝and running under APL64, 
 :andif ~apl64_dev≡GETNAME                          ⍝and not on development machine,
    A⍙⍙←¯1                                          ⍝   report error and save workspace
 :end

 ⎕ELX←'⎕DM'
 Z⍙⍙←0
 :if A⍙⍙=0                                          ⍝If not trapping errors,
    ⍎X⍙⍙                                            ⍝   just execute it
 :else
       ⎕ELX←(1 ¯1⍳A⍙⍙)⊃'⎕ERROR ⎕DM' 'REPORT'        ⍝Otherwise, trap errors, or just report them, depending
    :Try
       ⍎X⍙⍙
    :CatchAll
       ⎕ERROR ('BREAK'≡5↑⎕DM)/(¯1+⎕DM⍳⎕TCNL)↑⎕DM    ⍝   Don't trap errors generated by BREAKCHECK
       Z⍙⍙←1
    :EndTry
 :end